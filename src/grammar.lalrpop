// -*- mode: rust -*-
#![cfg_attr(rustfmt, rustfmt_skip)]
use std::str::FromStr;
use ast::{Program, Statement, Expression, Binop, Variable};

grammar;

Comma<T>: Vec<T> = {
    <v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut vm = v;
            vm.push(e);
            vm
        }
    }
};

pub program: Program = {
    <declaration*> => Program::new(<>),
};

declaration: Statement = {
    "var" <id:identifier> <e:("=" <expression>)?> ";" =>
        Statement::Var(Variable::Unresolved(String::from(id)), e),
    "fun" <identifier> "(" <Comma<identifier>> ")" "{" <declaration*> "}" =>
        Statement::Fun(<>),
    statement,
};

statement = statement1<"I">;

statement1<I>: Statement = {
    "print" <expression> ";" => Statement::Print(<>),
    "{" <declaration*> "}" => Statement::Block(<>),
    "while" "(" <e:expression> ")" <s:statement1<I>> =>
        Statement::While(e, Box::new(s)),
    "if" "(" <c:expression> ")" <s:statement1<"">> "else" <e:statement1<I>> =>
        Statement::If(c, Box::new(s), Box::new(Some(e))),
    "if" "(" <c:expression> ")" <s:statement1<I>> if I != "" =>
        Statement::If(c, Box::new(s), Box::new(None)),
    "return" <expression?> ";" => Statement::Return(<>),
    <expression> ";" => Statement::Expression(<>),
};

Tier<Op, NextTier>: Expression = {
    <lhs:Tier<Op, NextTier>> <op:Op> <rhs:NextTier>
        => Expression::Binop(op, Box::new(lhs), Box::new(rhs)),
    NextTier
};

expression = assignment;

assignment = Tier<assignment_op, equality>;
equality = Tier<equality_op, comparison>;
comparison = Tier<comparison_op, addition>;
addition = Tier<addition_op, multiplication>;
multiplication = Tier<multiplication_op, call>;

assignment_op: Binop = "=" => Binop::Assignment;
equality_op: Binop = {
    "==" => Binop::Equal,
    "!=" => Binop::NotEqual,
};
comparison_op: Binop = {
    ">" => Binop::Greater,
    ">=" => Binop::GreaterEqual,
    "<" => Binop::Less,
    "<=" => Binop::LessEqual,
};
addition_op: Binop = {
    "+" => Binop::Add,
    "-" => Binop::Sub,
};
multiplication_op: Binop = {
    "*" => Binop::Mul,
    "/" => Binop::Div,
};

call: Expression = {
    <ca:primary> "(" <arg:Comma<expression>> ")" => Expression::Call(Box::new(ca), arg),
    primary,
};

primary: Expression = {
    <num>,
    identifier => Expression::Variable(Variable::Unresolved(String::from(<>))),
    "(" <expression> ")"
};

identifier: String =
   r"\pL[\pL\pN_]*" => String::from(<>);

num: Expression =
    r"[0-9]+(\.[0-9]+)?" => Expression::Literal(f64::from_str(<>).unwrap());

// vi: ft=rust
